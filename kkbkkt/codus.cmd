# coding:cp866
#-----------------------------------------------------------------------------
#  Комманды этого файла вызываются 18 раз/секунду (каждый пользовательский тик)
#  p0000 - (0) переменные общего массива (базы каналов)
#  l0000 - (1) переменные локального массива
#  c0000 - переменные массива COM порта
#  n0000 - переменные массива NetBIOS
#  t0000 - переменные массива TCPIP
#  y0000 - (6) переменные массива комманды 'y'
#  x0000 - (7) временные  переменные
#-----------------------------------------------------------------------------


#  ОПРЕДЕЛЕНИЕ ПЕРЕМЕННЫХ:
#-----------------------------------------------------------------------------
   %РУЧ            = 0                      ;
   %АВТ            = 1                      ;
   %ВЫК            = 0                      ;
   %ВКЛ            = 1                      ;
   %НЕТ            = 0                      ;
   %ДА             = 1                      ;
#--------------------------------------------
   %ПАРОЛЬ_К               = 787878         ;
   %ПАРОЛЬ_Т               = 454545         ;
   %ПАРОЛЬ_СТАРТ           = 12             ;
   %ПАРОЛЬ_СТОП            = 56             ;
   %ПАРОЛЬ_ВСЕ_ВЫКЛ        = 90             ;
   %ПАРОЛЬ_СТАРТ_ЗАП       = x0031          ;
   %ПАРОЛЬ_СТОП_ЗАП        = x0032          ;
   %ПАРОЛЬ_ВСЕ_ВЫКЛ_ЗАП    = x0033          ;
#--------------------------------------------
   %КЛАВИША_У              = 5              ;
   %КЛАВИША_Д              = 12             ;
   %КЛАВИША_С              = 3              ;
   %КЛАВИША_S              = 19             ;
   %КЛАВИША_F11            = 57             ;
   %КЛАВИША_F12            = 58             ;
   %КЛАВИША_ESC            = 59             ;
   %КЛАВИША_ENTER          = 67             ;
   %КЛАВИША_ZERO           = 0              ;
   %ТЕКУЩИЙ_ЦИКЛ_ЗВОНКА    = x0023          ;
   %ТЕКУЩИЙ_ЦИКЛ_ЗАПИСИ    = x0024          ;
#--------------------------------------------
   %КЛАВИША_МЕНЮ           = l0004          ;
   %КЛАВИША_УСТАНОВКА      = l0032          ;
   %НАЖАТАЯ_КЛАВИША        = l0007          ;
   %ГЕНЕРИРУЕМАЯ_КЛАВИША   = l0005          ;
   %ВИРТУАЛЬНАЯ_КЛАВИША    = x0030          ;
   %ВВОД_ДАННЫХ            = l0006          ;
#--------------------------------------------
   %ФЛАГ_ПЕРЕДАЧИ_ЗНАЧЕНИЯ = l0019          ;
   %СТРОК_В_ТЕКУЩЕМ_ФАЙЛЕ  = l0069          ;
   %СТРОК_В_ТФЛ_ДЛЯ_КОДУС  = y0069          ;
#--------------------------------------------
   %СЕКУНДА_НОВАЯ          = x0101          ;
   %СЕКУНДА_ЗАПИС          = x0100          ;
   %СЕКУНДА_КОДУС          = l0041          ;
   %СЧЕТЧИК_ТИКОВ          = x0001          ;
   %СИГНАЛИЗАЦИЯ           = l0043          ;
   %СИГНАЛИЗАЦИЯ_1         = x0002          ;
   %СИГНАЛИЗАЦИЯ_ФЛАГ      = x0003          ;
#--------------------------------------------
   %НОМЕР_ЭКРАНА           = l0031          ;
   %ФЛАГ_КОНТРОЛЯ_ЭКРАНА   = x0025          ;
   %ФЛАГ_ЗАПРЕТА_ИЗМЕНЕНИЙ = l0020          ;
   %ФЛАГ_ЗАПРЕТА_МЕНЮ      = l0021          ;
   %ПАРОЛЬ_ВВОД__КИП       = l0045          ;
   %ПАРОЛЬ_ТЕКУЩ_КИП       = l0046          ;
   %ПАРОЛЬ_ВВОД__ТЕХ       = l0047          ;
   %ПАРОЛЬ_ТЕКУЩ_ТЕХ       = l0048          ;
#--------------------------------------------
   %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = l0033          ;
   %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = l0034          ;
   %СООБЩЕНИЕ_ОШИБКИ       = l0035          ;
#--------------------------------------------


#  ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ:
#-----------------------------------------------------------------------------
#  определение паролей
   %ПАРОЛЬ_ТЕКУЩ_КИП       = %ПАРОЛЬ_К      ;
   %ПАРОЛЬ_ТЕКУЩ_ТЕХ       = %ПАРОЛЬ_Т      ;
#-----------------------------------------------------------------------------
#  макс кол строк текущ файла просмотра (для вывода кол строк)
   %СТРОК_В_ТФЛ_ДЛЯ_КОДУС = %СТРОК_В_ТЕКУЩЕМ_ФАЙЛЕ    ;
#-----------------------------------------------------------------------------
#  установка флага новой секунды
   %СЕКУНДА_НОВАЯ = %ВЫК                    ;
if %СЕКУНДА_ЗАПИС ! %СЕКУНДА_КОДУС          ;
   %СЕКУНДА_НОВАЯ = %ВКЛ                    ;
fi                                          ;
   %СЕКУНДА_ЗАПИС = %СЕКУНДА_КОДУС          ;
#-----------------------------------------------------------------------------
#  каждую новую секунду                     ;
if %СЕКУНДА_НОВАЯ = %ВКЛ                    ;
   x0047 = x0047 + 1
fi                                          ;
#----------
#  установка переменных один раз в начале программы
if l0014 = 1                                ; 
   l0014 = 2                                ; Флаг запуска программы CODUS
   x0046 = 0                                ; Шаг генерации отчета
   x0047 = 1                                ; Счетчик секунд работы программы
   x0048 = 0                                ; Флаг генерации отчета
   x0049 = 1                                ; Счетчик секунд в генерации отчета
   l0077 = 1
fi                                          ;
#  каждые новые сутки работы программы
if x0047 = 86400                            ;
   x0047 = 1                                ;
   x0049 = 1                                ;
fi                                          ;
#-----------------------------------------------------------------------------


#  ОТОБРАЖЕНИЕ ЗНАКА "ВНИМАНИЕ" ПРИ ОШИБКАХ:
#-----------------------------------------------------------------------------
   %СИГНАЛИЗАЦИЯ_ФЛАГ = %ВЫКЛ          ;
if $ErrBell1_V ! %ВЫКЛ                 ; # если установлен флаг звонка
   %СИГНАЛИЗАЦИЯ_ФЛАГ = %ВКЛ           ;
fi                                     ;
if $ErrBell3_V ! %ВЫКЛ                 ; 
   %СИГНАЛИЗАЦИЯ_ФЛАГ = %ВКЛ           ;
fi                                     ;
if $ErrBell5_V ! %ВЫКЛ                 ; 
   %СИГНАЛИЗАЦИЯ_ФЛАГ = %ВКЛ           ;
fi                                     ;
if %СИГНАЛИЗАЦИЯ_ФЛАГ ! %ВЫКЛ          ; # если установлен флаг звонка -
   %СЧЕТЧИК_ТИКОВ = %СЧЕТЧИК_ТИКОВ + 1 ; # счетать тики
el                                     ;
   %СИГНАЛИЗАЦИЯ  = %ВЫКЛ              ; # перемен из локал базы 
fi                                     ; # для вывода анимированного значка (!)
if %СЧЕТЧИК_ТИКОВ   >  9               ;
   %СЧЕТЧИК_ТИКОВ   =  0               ; # каждые пол секунды менять 
   if %СИГНАЛИЗАЦИЯ >  0               ; # изображение (мигающий значек)
      %СИГНАЛИЗАЦИЯ =  0               ;
   el                                  ;
      if %СИГНАЛИЗАЦИЯ_1 = 1           ;
         %СИГНАЛИЗАЦИЯ   = 2           ;
      el                               ;
         %СИГНАЛИЗАЦИЯ   = 1           ;
      fi                               ;
      %СИГНАЛИЗАЦИЯ_1 = %СИГНАЛИЗАЦИЯ  ;
   fi                                  ;
fi                                     ;
#-----------------------------------------------------------------------------


# КОНТРОЛЬ ДОСТУПА К ЭКРАНАМ ПО ПАРОЛЮ:
#-----------------------------------------------------------------------------
%ФЛАГ_ЗАПРЕТА_ИЗМЕНЕНИЙ = 0  ;    # флаг запрета изменений
%ФЛАГ_ЗАПРЕТА_МЕНЮ      = 0  ;    # флаг запрета меню

%ФЛАГ_КОНТРОЛЯ_ЭКРАНА    = %ВЫКЛ            ; # экран контролируется паролем

if %НОМЕР_ЭКРАНА = 3                        ; # экран "СИСТЕМА" (E 3)
   %ФЛАГ_КОНТРОЛЯ_ЭКРАНА = %ВКЛ             ;
fi                                          ;
if %НОМЕР_ЭКРАНА = 8                        ; # экран "МОДУЛИ"
   %ФЛАГ_КОНТРОЛЯ_ЭКРАНА = %ВКЛ             ;
fi                                          ;
if %НОМЕР_ЭКРАНА = 4                        ; # экран "ДИАПАЗОН" | "ОТЛАДКА"
   %ФЛАГ_КОНТРОЛЯ_ЭКРАНА = %ВКЛ             ;
fi                                          ;
if %НОМЕР_ЭКРАНА = 5                        ; # экран "СИГНАЛИЗАЦИЯ"
   %ФЛАГ_КОНТРОЛЯ_ЭКРАНА = %ВКЛ             ;
fi                                          ;
#-----------------------------------------------------------------------------


#  ПЕРЕХОД НА ЭКРАНЫ "ТЕСТ1" и "ТЕСТ2"
#-----------------------------------------------------------------------------
if %НАЖАТАЯ_КЛАВИША = %КЛАВИША_F11           ; # выбран переход на экран "ТЕСТ1"
   if %ПАРОЛЬ_ТЕКУЩ_КИП  ! %ПАРОЛЬ_ВВОД__КИП ; # если не введен пароль КИП и А
      %НАЖАТАЯ_КЛАВИША   = %КЛАВИША_ZERO     ;
      %КЛАВИША_УСТАНОВКА = %ВКЛ              ; # клавиши (никаких действий)
      %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 4            ;
      %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10           ;
      %СООБЩЕНИЕ_ОШИБКИ  = %ВКЛ              ;
   fi                                        ;
fi                                           ;
#---------------------------------------------
if %НАЖАТАЯ_КЛАВИША = %КЛАВИША_F12           ; # выбран переход на экран "ТЕСТ2"
   if %ПАРОЛЬ_ТЕКУЩ_КИП  ! %ПАРОЛЬ_ВВОД__КИП ;
      %НАЖАТАЯ_КЛАВИША   = %КЛАВИША_ZERO     ;
      %КЛАВИША_УСТАНОВКА = %ВКЛ              ;
      %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 4            ;
      %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 60           ;
      %СООБЩЕНИЕ_ОШИБКИ  = %ВКЛ              ;
   fi                                        ;
fi                                           ;
#-----------------------------------------------------------------------------


#  при нажатии клавиши ESC после закрытия окна ввода или запроса-
#  снимается звуковой сигнал
#-----------------------------------------------------------------------------
if %ВИРТУАЛЬНАЯ_КЛАВИША > 0                      ; # после нажат клавиши ESC 
   if %СЕКУНДА_НОВАЯ    = %ДА                    ; # генерируется
      %НАЖАТАЯ_КЛАВИША  = %ВИРТУАЛЬНАЯ_КЛАВИША   ; # нажатие клавиши 'S' для 
      %ВИРТУАЛЬНАЯ_КЛАВИША  = %КЛАВИША_ZERO      ; # снятия сигнала ошибки
      %КЛАВИША_УСТАНОВКА    = %ВКЛ               ;
      %ГЕНЕРИРУЕМАЯ_КЛАВИША = %КЛАВИША_ZERO      ; # сбросить код посл генерир 
   fi                                            ; # клавиши запр оператора
fi                                               ;
if %НАЖАТАЯ_КЛАВИША = %КЛАВИША_ESC               ; # если нажата клавиша ESC
   %ВИРТУАЛЬНАЯ_КЛАВИША  = %КЛАВИША_S            ; # установить нажатие клавиши
fi                                               ; # 'S' для  след цикла
if %ГЕНЕРИРУЕМАЯ_КЛАВИША = %КЛАВИША_ESC          ; # клавиш ESC генерир запрос
   %ВИРТУАЛЬНАЯ_КЛАВИША  = %КЛАВИША_S            ; # установить нажатие клавиши 
fi                                               ; # 'S' для след цикла
#-----------------------------------------------------------------------------



#  ПРОВЕРКА ЗАКРЫТИЯ ЭКРАНОВ ПАРОЛЕМ:
#-----------------------------------------------------------------------------
if %ВВОД_ДАННЫХ  = %ВЫКЛ                          ;
   if %ПАРОЛЬ_ТЕКУЩ_КИП  ! %ПАРОЛЬ_ВВОД__КИП      ;
      if %ФЛАГ_КОНТРОЛЯ_ЭКРАНА   = %ВКЛ           ;
         %ФЛАГ_ЗАПРЕТА_ИЗМЕНЕНИЙ = %ВКЛ           ;
         %ФЛАГ_ЗАПРЕТА_МЕНЮ      = %ВКЛ           ;
         if %НАЖАТАЯ_КЛАВИША     = %КЛАВИША_ENTER ;
            %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 5           ;
            %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10          ;
            %СООБЩЕНИЕ_ОШИБКИ    = %ВКЛ           ;
         fi                                       ;
      fi                                          ;
      # Если нажата клавиша 'Д'(ДИАПАЗОНЫ) и не введен пароль- предупреждение
      if %НАЖАТАЯ_КЛАВИША        = %КЛАВИША_Д     ;        
         %НАЖАТАЯ_КЛАВИША        = %КЛАВИША_ZERO  ;
         %КЛАВИША_УСТАНОВКА      = %ВКЛ           ;
         %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 4              ;
         %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10             ;
         %СООБЩЕНИЕ_ОШИБКИ       = %ВКЛ           ;
      fi                                          ;
   fi                                             ;
   if %ПАРОЛЬ_ТЕКУЩ_ТЕХ  ! %ПАРОЛЬ_ВВОД__ТЕХ      ;
      if %ФЛАГ_КОНТРОЛЯ_ЭКРАНА   = 2              ;
         %ФЛАГ_ЗАПРЕТА_ИЗМЕНЕНИЙ = %ВКЛ           ;
         %ФЛАГ_ЗАПРЕТА_МЕНЮ      = %ВКЛ           ;
         if %НАЖАТАЯ_КЛАВИША     = %КЛАВИША_ENTER ;
            %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 7           ;
            %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10          ;
            %СООБЩЕНИЕ_ОШИБКИ    = %ВКЛ           ;
         fi                                       ;
      fi                                          ;
      # Если нажата клавиша 'С'(СИГНАЛИЗАЦИЯ) и не введен пароль-предупреждение
      if %НАЖАТАЯ_КЛАВИША        = %КЛАВИША_С     ;        
         %НАЖАТАЯ_КЛАВИША        = %КЛАВИША_ZERO  ;
         %КЛАВИША_УСТАНОВКА      = %ВКЛ           ;
         %НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 6              ;
         %СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10             ;
         %СООБЩЕНИЕ_ОШИБКИ       = %ВКЛ           ;
      fi                                          ;
   fi                                             ;
fi                                                ;
#-----------------------------------------------------------------------------


# Автоматическая генерация отчетов и запись их в файл
# -----------------------------------------------------------------------------
%ГЕНЕРАЦИЯ_ЧАС  = 7
%ГЕНЕРАЦИЯ_МИН1 = 7
%ГЕНЕРАЦИЯ_МИН2 = 8
%ГЕНЕРАЦИЯ_МИН3 = 9
%ГЕНЕРАЦИЯ_СЕК  = 7

# ---------                                 ;
# Начало генерации отчета                   ;
if x0046 = 0                                ;
if l0039 = %ГЕНЕРАЦИЯ_ЧАС                   ; Час
if l0041 = %ГЕНЕРАЦИЯ_СЕК                   ; Секунда

l0007 = 0                                   ;
if l0040 = %ГЕНЕРАЦИЯ_МИН1                  ; Минута
l0007 = 446                                 ; Код нажатой клавиши (Меню - Котел1)
fi                                          ; 

if l0040 = %ГЕНЕРАЦИЯ_МИН2                  ; Минута
l0007 = 447                                 ; Код нажатой клавиши (Меню - Котел2)
fi                                          ;

if l0007 > 0                                ; 
l0032 = 1                                   ; Флаг ввода кода нажатой клавиши
x0046 = 1                                   ; Следующий шаг генерации отчета
x0049 = x0047                               ; Текущий счетчик новой секунды
x0048 = 1                                   ; 
fi                                          ;

fi                                          ;
fi                                          ;
fi                                          ;

# ---------
# Ввод дня недели:                          ;
if x0046 = 1                                ; Шаг генерации
if x0049 ! x0047                            ; Если прошла секунда
   o_TEST_LOC_018:#1.0018#                  ;
   x0050 = l0018 - 1                        ; Предыдущий день недели
if x0050 < 0                                ;
   x0050 = 6                                ;
fi                                          ;
$CurDayWk_M = x0050                         ; День недели за который нужно сгенерировать отчет
x0046 = 2                                   ; Следующий шаг генерации отчета
x0049 = x0047 + 15                          ; Текущий счетчик новой секунды (ждать 15сек для изменения всех переменных)
o_CurWeekDay_M:#CurDayWk_M# 
o_CurWeekDay_V:#CurDayWk_V# 
o_TEST_TMP_050:#7.0050# 
o_TEST_TMP_047:#7.0047# 
fi                                          ;
fi                                          ;

# ----------                                ;
# Нажать кнопку "ГЕНЕРАЦИЯ" на экране       ;
if x0046 = 2                                ; Шаг генерации
if x0049 < x0047                            ; Если прошел обмен по сети
l0007 = 303                                 ; Код нажатой клавиши
l0032 = 1                                   ; Флаг ввода кода нажатой клавиши
x0046 = 3                                   ; Следующий шаг генерации отчета
x0049 = x0047                               ; Текущий счетчик новой секунды
fi                                          ;
fi                                          ;

# ---------                                 ;
# Нажать кнопку "ПЕЧАТЬ" на экране          ;
if x0046 = 3                                ; Шаг генерации
if x0049 ! x0047                            ; Если прошел обмен по сети
l0007 = 716                                 ; Код нажатой клавиши
l0032 = 1                                   ; Флаг ввода кода нажатой клавиши
x0046 = 0                                   ;
fi                                          ;
fi                                          ;

if l0040 = %ГЕНЕРАЦИЯ_МИН3                  ;
if x0048 > 0                                ;
x0048 = 0                                   ;
l0007 = 48                                  ; Код нажатой клавиши (F2 - Таблица1)
l0032 = 1                                   ; Флаг ввода кода нажатой клавиши
fi                                          ;
fi                                          ;

if x0048 > 0                                ;
%НОМЕР_СООБЩЕНИЯ_ВЫВОДА = 8              ;
%СЕК_ОТОБРАЖ_СООБЩЕНИЯ  = 10             ;
%СООБЩЕНИЕ_ОШИБКИ       = %ВКЛ           ;
fi                                          ;
#-----------------------------------------------------------------------------


#-----------------------------------------------------------------------------
# пересчет Гкал для текущего отчета
x0011 = $AIF301_D_V * 0.563  ;    # котел 1
x0012 = $AIF311_D_V * 0.563  ;    # котел 2
#-----------------------------------------------------------------------------
p1075 = p0055 ;
#-----------------------------------------------------------------------------
