#-----------------------------------------------------------------------------
#  The commands in this file are called 18 times/second (every user tick)
#  x0000 - (7) temporary variables
#  p0000 - (0) common array variables (channel bases)
#  l0000 - (1) local array variables
#  c0000 - COM port array variables
#  n0000 - NetBIOS array variables
#  t0000 - TCPIP array variables
#  y0000 - command array variables 'y'

#-----------------------------------------------------------------------------
#  Definition of variables
   %РУЧ = 0                                 ;
   %АВТ = 1                                 ;
   %ВЫК = 0                                 ;
   %ВКЛ = 1                                 ;
   %КРАСНЫЙ = 0                             ;
   %СИНИЙ   = 1                             ;
   %ГОЛУБОЙ = 1                             ;
   %ЗЕЛЕНЫЙ = 2                             ;
   %ЖЕЛТЫЙ  = 3                             ;
   %СТРОК_В_ТЕКУЩЕМ_ФАЙЛЕ = l0069           ;
   %СТРОК_В_ТФЛ_ДЛЯ_КОДУС = y0069           ;
   %СЕКУНДА_НОВАЯ = x0101                   ;
   %СЕКУНДА_ЗАПИС = x0100                   ;
   %СЕКУНДА_КОДУС = l0041                   ;
   %ДЗ_СУХОГО  = x0081                      ;
   %ДЗ_МОКРОГО = x0082                      ;
   %ДЗ_АПАСТЫ  = x0083                      ;
#-----------------------------------------------------------------------------

#-----------------------------------------------------------------------------
#  max number of lines of the current view file (to display the number of lines)
   %СТРОК_В_ТФЛ_ДЛЯ_КОДУС = %СТРОК_В_ТЕКУЩЕМ_ФАЙЛЕ    ;
#-----------------------------------------------------------------------------

#-----------------------------------------------------------------------------
   %СЕКУНДА_НОВАЯ = %ВЫК                    ;
if %СЕКУНДА_ЗАПИС ! %СЕКУНДА_КОДУС          ;
   %СЕКУНДА_НОВАЯ = %ВКЛ                    ;
fi                                          ; 
   %СЕКУНДА_ЗАПИС = %СЕКУНДА_КОДУС          ;
#-----------------------------------------------------------------------------

#-----------------------------------------------------------------------------
#  Status of dosing controls for highlighting (S M P)
if $REG06GL__V = %РУЧ                       ;
   %ДЗ_СУХОГО  = %ГОЛУБОЙ                   ;
el                                          ;
   %ДЗ_СУХОГО  = %ЗЕЛЕНЫЙ                   ;
fi                                          ;
if $REG06R___V = %ВЫК                       ;  
   %ДЗ_СУХОГО  = %КРАСНЫЙ                   ;
fi                                          ;
#--------------------------------------------
if $REG07GL__V = %РУЧ                       ;
   %ДЗ_МОКРОГО = %ГОЛУБОЙ                   ;
el                                          ;
   %ДЗ_МОКРОГО = %ЗЕЛЕНЫЙ                   ;
fi                                          ;
if $REG07R___V = %ВЫК                       ;
   %ДЗ_МОКРОГО = %КРАСНЫЙ                   ;
fi                                          ;
#--------------------------------------------
if $REG08GL__V = %РУЧ                       ;
   %ДЗ_АПАСТЫ  = %ГОЛУБОЙ                   ;
el                                          ;
   %ДЗ_АПАСТЫ  = %ЗЕЛЕНЫЙ                   ;
fi                                          ;
if $REG08R___V = %ВЫК                       ;
   %ДЗ_АПАСТЫ  = %КРАСНЫЙ                   ;
fi                                          ;
#--------------------------------------------
if $REG09R___V    = %РУЧ                    ;
   if $REG08___V  = %РУЧ                    ;
      %ДЗ_АПАСТЫ  = %КРАСНЫЙ                ;
   fi                                       ;
el                                          ;
   if $REG09GL__V = %ВЫК                    ;
      %ДЗ_АПАСТЫ  = %ГОЛУБОЙ                ;
   el                                       ;
      %ДЗ_АПАСТЫ  = %ЗЕЛЕНЫЙ                ;
   fi                                       ;
fi                                          ;
#-----------------------------------------------------------------------------

#-----------------------------------------------------------------------------
l0020 = 0 ;                  # change disable flag
l0021 = 0 ;                  # menu disable flag
l0046 = 787878 ;             # master password
l0048 = 454545 ;             # technologist password

x0021 = 0 ;                  # inscription color when the MASTER password is entered
x0022 = 0 ;                  # the color of the inscription when the TECHNOLOGY password is entered

   x0041 = 0       ;         # output for selection operator PASTE-POWDER
   x0042 = 0       ;  
if $TRPAS__VIB > 0 ;         # PASTE
   x0041 = 1       ;
fi                 ;
if $TRPUD__VIB > 0 ;         # POWDER
   x0042 = 1       ;
fi                 ;

if l0045 ! l0046 ;            
   x0021 = 8 ;               # the color of the inscription when the MASTER password is not entered
fi ;
if l0047 ! l0048 ;           
   x0022 = 8 ;               # inscription color when TECHNOLOGY password is not entered
fi ;

x0025 = 0 ;                  # screen is password controlled
if l0031 = 6 ;               # "SETTINGS" screen
   x0025 = 1 ;
fi ;
if l0031 = 4 ;               # "RANGE" screen
   x0025 = 1 ;
fi ;
if l0031 = 8 ;               # "I7042 MODULES" screen
   x0025 = 1 ;
fi ;
if l0031 = 7 ;               # "CONTROL SETTINGS" screen
   x0025 = 1 ;
fi ;
if l0031 = 5 ;               # "ALARM" screen
   x0025 = 2 ;
fi ;


#-----------------------------------------------------------------------------
# initial setting of variable values
if x0000 = 0 ;
   x0000 = 1 ;
fi ;

#-----------------------------------------------------------------------------
# transmission of the message number (taken from the variable N14 of the common channel base)
l0010 = p0014 ;

#-----------------------------------------------------------------------------
# COM port and NetBIOS network operation flag, which are transmitted to and received by the CS
x0003 = x0003 + 1 ;
if x0003 > 10 ;
   x0003 =  0 ;

   p0029 = p0029 + 1 ;                      # COM
   p0030 = p0030 + 1 ;                      # NBW
   if p0029 > 1000 ;                        # COM
      p0029 = 1 ;
   fi ;
   if p0030 > 1000 ;                        # NBW
      p0030 = 1 ;
   fi ;

   if x0004 ! p0031 ;                       # COM
      x0005 = x0005 + 1 ;
      if x0005 > 8  ;
         x0005 = 1  ;
      fi ;
      x0004 = p0031 ;
      x0006 = 0 ;
   el ;
      x0006 = x0006 + 1 ;
      if x0006 > 20 ;
         x0005 =  0 ;
      fi ;
   fi ;

   if x0007 ! p0032 ;                       # NBW
      x0008 = x0008 + 1 ;
      if x0008 > 8  ;
         x0008 = 1  ;
      fi ;
      x0007 = p0032 ;
      x0009 = 0 ;
   el ;
      x0009 = x0009 + 1 ;
      if x0009 > 10 ;
         x0008 =  0 ;
      fi ;
   fi ;
fi ;

#-----------------------------------------------------------------------------
# flag for displaying error messages (displaying the ATTENTION sign)
if p0020 > 0 ;                         # if call flag is set
   x0001 = x0001 + 1 ;                 # tick count time variable
el ;
   l0043 =  0 ;                        # changes from base locale for animation output
fi ;
if x0001 >  9 ;
   x0001 =  0 ;
   if l0043 > 0 ;
      l0043 = 0 ;
   el ;
      if x0002 = 1 ;
         l0043 = 2 ;
      el ;
         l0043 = 1 ;
      fi ;
      x0002 = l0043 ;
   fi ;
fi ;

#-----------------------------------------------------------------------------
# CPU usage display color
   l0044 = %ЖЕЛТЫЙ   ;
if l0001 < 50000     ;
   l0044 = %ЗЕЛЕНЫЙ  ;
fi                   ;
if l0001 < 20000     ;
   l0044 = %СИНИЙ    ;
fi                   ;
if l0001 < 1000      ;
   l0044 = %КРАСНЫЙ  ;
fi                   ;
#-----------------------------------------------------------------------------
if l0031 = 1 ;               # если это экран N1 (СХЕМА АСУТП)
   if l0007 = 64 ;           # если нажата клавиша TAB
      l0007 = 56 ;           # установить нажатие клавиши F10 (СХЕМА Каналы)
      l0032 = 1  ;           # установить флаг обработки нажатия в КОДУСе
   fi ;
fi ;
if l0031 = 2 ;               # если это экран N2 (СХЕМА Каналы)
   if l0007 = 64 ;           # если нажата клавиша TAB
      l0007 = 48 ;           # установить нажатие клавиши F2  (СХЕМА АСУТП)
      l0032 = 1  ;           # установить флаг обработки нажатия в КОДУСе
   fi ;
fi ;

#-----------------------------------------------------------------------------
#  при нажатии клавиши ESC после закрытия окна ввода или запроса- 
#  снимается звуковой сигнал
if x0030 > 0            ;    # после нажатия клавиши ESC генерируется
   if x0101 = 1         ;    
      l0007 = x0030     ;    # нажатие клавиши 'S' для снятия сигнала ошибки
      x0030 = 0         ;    
      l0032 = 1         ;    # установить флаг обработки нажатия в КОДУСе
      l0005 = 0         ;    # сбросить код посл генерир клавиши запр оператора
   fi                   ;
fi                      ;    
if l0007 = 59           ;    # если нажата клавиша ESC
   x0030 = 19           ;    # установить нажатие клавиши 'S' для след цикла
fi                      ;    
if l0005 = 59           ;    # клавиша ESC сгенерир запросом оператора
   x0030 = 19           ;    # установить нажатие клавиши 'S' для след цикла
fi                      ;    

#-----------------------------------------------------------------------------
if l0007 = 57 ;                        # если выбран переход на экран "ТЕСТ1"
   if l0045 ! l0046 ;
      l0007 = 0 ;
      l0032 = 1 ;
   fi ;
fi ;
if l0007 = 58 ;                        # если выбран переход на экран "ТЕСТ2"
   if l0045 ! l0046 ;
      l0007 = 0 ;
      l0032 = 1 ;
   fi ;
fi ;

#-----------------------------------------------------------------------------

if l0006 = 0 ;               # Если нет ввода данных

   if l0045 ! l0046 ;        # Если не введен пароль КИП и А
      if x0025 = 1 ;         # если это контролируемый экран
         l0020 = 1 ;         # запретить действия меню
         l0021 = 1 ;         # запретить изменения
         if l0007 = 67 ;     # если нажата клавиша ENTER -
            l0033 = 5  ;     # вывести предупредительное сообщение
            l0035 = 1  ;     # вывести сообщение, как ошибку
         fi ;
      fi ;
      if l0007 = 12 ;        # Если нажата клавиша 'Д'- (ДИАПАЗОНЫ)
         l0007 = 0  ;        # игнорировать это нажатие.
         l0032 = 1  ;        # Установить флаг обработки нажатия в КОДУСе
         l0033 = 4  ;        # Вывести предупредительное сообщение.
         l0034 = 10 ;        # Отображение сообщения 10 сек
         l0035 = 1  ;        # Вывести сообщение, как ошибку.
      fi ;
      if l0007 = 5  ;        # Если нажата клавиша 'У'- (УСТАНОВКИ)
         l0007 = 0  ;        # игнорировать это нажатие.
         l0032 = 1  ;        # Установить флаг обработки нажатия в КОДУСе
         l0033 = 4  ;        # вывести предупредительное сообщение.
         l0034 = 10 ;        # Отображение сообщения 10 сек
         l0035 = 1  ;        # Вывести сообщение, как ошибку.
      fi ;
   fi ;

   if l0047 ! l0048 ;        # Если не введен пароль ТЕХНОЛОГА
      if x0025 = 2 ;         # если на экране "СИГНАЛИЗАЦИЯ"
         l0020 = 1 ;         # запретить действия меню
         l0021 = 1 ;         # запретить изменения
         if l0007 = 67 ;     # если нажата клавиша ENTER -
            l0033 = 7  ;     # вывести предупредительное сообщение
            l0035 = 1  ;     # вывести сообщение, как ошибку
         fi ;
      fi ;
      if l0007 = 3  ;        # Если нажата клавиша 'С'- (СИГНАЛИЗАЦИЯ)
         l0007 = 0  ;        # игнорировать это нажатие.
         l0032 = 1  ;        # Установить флаг обработки нажатия в КОДУСе
         l0033 = 6  ;        # вывести предупредительное сообщение.
         l0034 = 10 ;        # Отображение сообщения 10 сек
         l0035 = 1  ;        # Вывести сообщение, как ошибку.
      fi ;
   fi ;

fi ;
#-----------------------------------------------------------------------------
